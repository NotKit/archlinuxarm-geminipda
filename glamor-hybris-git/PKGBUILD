_pkgname=glamor-hybris
pkgname=$_pkgname-git
pkgver=r853.347463b
pkgrel=1
pkgdesc="glamor Xserver 2D acceleration modified to work with libhybris drivers"
url="https://github.com/NotKit/glamor-hybris"
arch=('x86_64' 'aarch64')
license=('MIT')
depends=('libepoxy' 'drihybris' 'libhybris' 'libxfont2')
makedepends=('git' 'pixman' 'xorg-server-devel' 'drihybrisproto')
provides=("$_pkgname")
source=('git+https://github.com/NotKit/glamor-hybris')
md5sums=('SKIP')

pkgver() {
	cd "$_pkgname"
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
	cd "$_pkgname"
	export LDFLAGS="$LDFLAGS -Wl,-z,lazy"
	export CPPFLAGS="$CPPFLAGS -I/usr/include/android -I/usr/include/libdrm"

	./autogen.sh \
		--build=$CBUILD \
		--host=$CHOST \
		--prefix=/usr \
		--enable-glamor-gles2
	make
}

package() {
	cd "$_pkgname"
	make DESTDIR="$pkgdir" install
	mv "$pkgdir/usr/include/xorg/glamor.h" "$pkgdir/usr/include/xorg/glamor-hybris.h"
	install -Dm644 COPYING "$pkgdir"/usr/share/licenses/$_pkgname/COPYING
}
